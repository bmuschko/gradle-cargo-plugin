apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'nexus'

def compatibilityVersion = 1.5
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion
group = 'org.gradle.api.plugins'
version = '0.6'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    groovy localGroovy()
    compile gradleApi()
    testCompile 'org.spockframework:spock-core:0.6-groovy-1.8'
    testCompile 'org.codehaus.cargo:cargo-ant:1.3.3'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Cargo plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
}

idea.project {
    jdkName = '1.6'
  
    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
}

modifyPom {
    project {
        name 'Gradle Cargo plugin'
        description 'Gradle plugin that provides deployment capabilities to local and remote containers via Cargo.'
        url 'https://github.com/bmuschko/gradle-cargo-plugin'
        inceptionYear '2011'

        scm {
            url 'https://github.com/bmuschko/gradle-cargo-plugin'
            connection 'scm:https://bmuschko@github.com/bmuschko/gradle-cargo-plugin.git'
            developerConnection 'scm:git://github.com/bmuschko/gradle-cargo-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'bmuschko'
                name 'Benjamin Muschko'
                email 'benjamin.muschko@gmail.com'
            }
        }
    }
}
