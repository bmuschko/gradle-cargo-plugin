apply plugin: 'groovy'
apply plugin: 'idea'

def compatibilityVersion = 1.5
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion
version = '0.5.2'

repositories {
    mavenCentral()
}

dependencies {
    groovy localGroovy()
    compile gradleApi()
    testCompile "junit:junit:4.+"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Cargo plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version")
    }
}

idea.project {
    jdkName = '1.6'
  
    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-6'
}
